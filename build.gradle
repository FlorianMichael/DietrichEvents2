plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "me.champeau.jmh" version "0.7.0"
}

base {
    group = project.maven_group
    archivesName = project.maven_name
    version = project.maven_version
    description = project.maven_description
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:5.10.3")
    testImplementation "org.junit.jupiter:junit-jupiter"

    jmh "org.openjdk.jmh:jmh-core:1.36"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.36"
    jmhAnnotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:1.36"
}

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    // Rename the project's license file to LICENSE_<project_name> to avoid conflicts
    from("LICENSE") {
        rename {
            "${it}_${project.archivesBaseName}"
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    maxParallelForks Runtime.runtime.availableProcessors()
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = "https://maven.lenni0451.net/" + (project.version.endsWith("SNAPSHOT") ? "snapshots" : "releases")

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            name = "ossrh"
            def releasesUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = project.archivesBaseName
            groupId = project.group
            version = project.version

            from components.java

            pom {
                name = artifactId
                description = project.description
                url = "https://github.com/FlorianMichael/DietrichEvents2"
                licenses {
                    license {
                        name = "Apache-2.0 license"
                        url = "https://github.com/FlorianMichael/DietrichEvents2/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "FlorianMichael"
                        name = "EnZaXD"
                        email = "florian.michael07@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/FlorianMichael/DietrichEvents2.git"
                    developerConnection = "scm:git:ssh://github.com/FlorianMichael/DietrichEvents2.git"
                    url = "github.com/FlorianMichael/DietrichEvents2"
                }
            }
        }
    }
}

signing {
    setRequired false
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
